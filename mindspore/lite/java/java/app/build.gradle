apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.1"
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName project.hasProperty("LITE_VERSION") ? LITE_VERSION : ""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        sourceSets {
            main {
                jniLibs.srcDirs=['libs']
            }
        }
    }
    android.libraryVariants.all {
        variant->variant.outputs.all {
            String libname = project.hasProperty("LITE_LIBNAME") ? LITE_LIBNAME : "mindspore-lite"
            outputFileName = libname + ".aar"
        }
    }
    aaptOptions {
        noCompress = ['.so']
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
}

publishing {
    repositories {
        maven {
            url uri("build")
        }
    }
    publications {
        maven(MavenPublication) {
            String libname = project.hasProperty("LITE_LIBNAME") ? LITE_LIBNAME : "mindspore-lite"
            groupId = 'mindspore'
            artifactId = libname
            version = project.hasProperty("LITE_VERSION") ? LITE_VERSION : ""
            String path = project.buildDir.path + File.separator + "outputs" + File.separator + "aar" + File.separator + libname + ".aar";
            artifact(path)
        }
    }
}

